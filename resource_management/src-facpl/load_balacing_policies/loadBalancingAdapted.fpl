//Hyper1: localhost, Id;0, Type1:6, type2:4
//Hyper2: 192.168.1.11, Id: 6, [Type1:6, type2:4]
PolicySet Load_Balancing { permit-overrides
	policies:
	PolicySet Create_Policies{permit-overrides
  		target: equal("CREATE", action/action-id)
  		policies:
    	PolicySet SLA_Type1 { deny-unless-permit
			target:(equal("P_1", subject/profile-id) || equal("P_2", subject/profile-id))
        		&& equal("6", resource/vm-type)        	
			policies:
      		Rule hyper_1 (permit
        		target: less-than-or-equal(100, system/hyper1.availableResources)
        			&& less-than-or-equal(system/hyper2.availableResources, system/hyper1.availableResources)
        		obl: [M create(0, system/vm-name, 6)])   
        	Rule hyper_2 (permit
        		target: less-than-or-equal(100, system/hyper1.availableResources)
        			&& less-than-or-equal(system/hyper1.availableResources, system/hyper2.availableResources)
        		obl: [M create(6, system/vm-name, 6)])       
      		obl-d: [O log("Not enough available resources for TYPE_1 VMs")]
    	}

    	PolicySet SLA_Type2 { deny-unless-permit
      		target:equal("P_2", subject/profile-id)
        		&& equal("4", resource/vm-type)
      		policies:
        	Rule hyper_1_create (permit
          		target: less-than-or-equal(200, system/hyper1.availableResources)
        			&& less-than-or-equal(system/hyper2.availableResources, system/hyper1.availableResources)
          		obl: [M create(0, system/vm-name, 4)]
        	)
        	Rule hyper_2_create (permit
          		target: less-than-or-equal(200, system/hyper1.availableResources)
        			&& less-than-or-equal(system/hyper1.availableResources, system/hyper2.availableResources)
          		obl: [M create(6, system/vm-name, 4)]
        	)
        	Rule hyper_1_freeze (permit
          		target:(
              		(equal(0, system/hyper1.availableResources) &&
                  		less-than-or-equal(2, system/hyper1.vm1-counter))
              		|| (equal(100, system/hyper1.availableResources) &&
                  		less-than-or-equal(1, system/hyper1.vm1-counter))
            		)
          		obl:
            	[M freeze("0", subtract(200, system/hyper1.availableResources), "6")]
            	[M create(0, system/vm-name, 4)]
        	)
        	Rule hyper_2_freeze (permit
          		target:(
          			(equal(0, system/hyper2.availableResources) &&
                  		less-than-or-equal(2, system/hyper2.vm1-counter))
             		|| (equal(100, system/hyper2.availableResources) &&
                  		less-than-or-equal(1, system/hyper2.vm1-counter))
            	)
          		obl:
           		[M create(6, system/vm-name, 4)]
        	)
      	obl-d: [O log("Not enough available resources for TYPE_2 VMs")]
    	}
    	
    }

	PolicySet Release_Policies { permit-overrides
  		target:equal("RELEASE", action/action-id)
    			&& (equal("P_1", subject/profile-id) || equal("P_2", subject/profile-id))
  		policies:
    	Rule hyper_1_release (permit
      		target:( in (resource/vm-name, system/hyper1.vm-names))
      		obl: [M release("0", resource/vm-name)]
    	)
    	Rule hyper_2_release (permit
      		target:( in (resource/vm-name, system/hyper2.vm-names))
      		obl: [M release("6", resource/vm-name)]
    	)
  	}
}