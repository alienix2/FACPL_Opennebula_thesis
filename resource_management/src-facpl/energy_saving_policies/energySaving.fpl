//Hyper1: localhost, Id; , Type1:6, type2:3
//Hyper2: 192.168.1.11, [Type1:5, type2:2]
PolicySet Energy_Saving { permit-overrides
	policies:
	PolicySet Create_Policies{permit-overrides
  		target: equal("CREATE", action/action-id)
  		policies:
    	PolicySet SLA_Type1 { deny-unless-permit
			target:(equal("P_1", subject/profile-id) || equal("P_2", subject/profile-id))
        		&& equal("6", resource/vm-type)
			policies:
      		Rule hyper_1 (permit
        		target: less-than-or-equal(1, system/hyper1.availableResources))
        	Rule hyper_2 (permit
          		target: less-than-or-equal(1, system/hyper2.availableResources)
          		obl: [M create(5, system/vm-id, "3")])       
      		obl-d: [O log("Not enough available resources for TYPE_1 VMs")]
    	}

    	PolicySet SLA_Type2 { deny-unless-permit
      		target:equal("P_2", subject/profile-id)
        		&& equal("3", resource/vm-type)
      		policies:
        	Rule hyper_1_create (permit
          		target: less-than-or-equal(2, system/hyper1.availableResources)
          		obl: [M create(0, system/vm-id, "3")]
        	)
        	Rule hyper_2_create (permit
          		target: less-than-or-equal(2, system/hyper2.availableResources)
          		obl: [M create(5, system/vm-id, "3")]
        	)
        	Rule hyper_1_freeze (permit
          		target:(
              		(equal(0, system/hyper1.availableResources) &&
                  		less-than-or-equal(2, system/hyper1.vm1-counter))
              		|| (equal(1, system/hyper1.availableResources) &&
                  		less-than-or-equal(1, system/hyper1.vm1-counter))
            		)
          		obl:
            	[M freeze("localhost", subtract(2, system/hyper1.availableResources), "6")]
            	[M create(0, system/vm-id, "3")]
        	)
        	Rule hyper_2_freeze (permit
          		target:(
          			(equal(0, system/hyper2.availableResources) &&
                  		less-than-or-equal(2, system/hyper2.vm1-counter))
             		|| (equal(1, system/hyper2.availableResources) &&
                  		less-than-or-equal(1, system/hyper2.vm1-counter))
            	)
          		obl:
           		[M freeze("192.168.1.11", subtract(2, system/hyper2.availableResources), "6")]
           		[M create(5, system/vm-id, "3")]
        	)
      	obl-d: [O log("Not enough available resources for TYPE_2 VMs")]
    	}
    	
    }

	PolicySet Release_Policies { permit-overrides
  		target:equal("RELEASE", action/action-id)
    			&& (equal("P_1", subject/profile-id) || equal("P_2", subject/profile-id))
  		policies:
    	Rule hyper_1_release (permit
      		target:( in (resource/vm-id, system/hyper1.vm-ids))
      		obl: [M release("localhost", resource/vm-id)]
    	)
    	Rule hyper_2_release (permit
      		target:( in (resource/vm-id, system/hyper2.vm-ids))
      		obl: [M release("192.168.1.11", resource/vm-id)]
    	)
  	}
}

Request:{ prova1 
	(action/action-id , "RELEASE")
	(subject/profile-id, "P_1")
	(resource/vm-id, "451")
}

PAS { 
	Extended Indeterminate : false ;
	Java Package : "prova" ;
	Requests To Evaluate : prova1;
	pep: deny-biased
	pdp: deny-unless-permit

	include Energy_Saving
}
